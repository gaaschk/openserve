package loan

import org.gsoft.openserv.domain.loan.Loan
import org.gsoft.openserv.domain.loan.LoanTypeProfile
import org.gsoft.openserv.domain.payment.billing.BillingStatement
import org.springframework.context.ApplicationContext
import org.gsoft.openserv.buslogic.repayment.InitialDueDateCalculator
import org.gsoft.openserv.buslogic.repayment.FirstDueDateCalculator
import org.gsoft.openserv.buslogic.loan.LoanTypeLogic
import org.gsoft.openserv.buslogic.interest.LoanInterestRateFactory
import java.util.Date
import java.util.Calendar

global ApplicationContext springContext

declare LoanTypeProfileUpdated
	loan : Loan
end

rule "Update Initial Due Date"
	when
		$l : Loan(initialDueDate == null, effectiveLoanTypeProfile != null, firstDueDate != null)
	then
		System.out.println("set the initial due date");
		InitialDueDateCalculator calculator = springContext.getBean(InitialDueDateCalculator.class);
		System.out.println("InitialDueDateCalculator: " + calculator);
		calculator.updateInitialDueDate($l);
		update($l);
end

rule "Update First Due Date"
	when
		$l : Loan(firstDueDate == null, effectiveLoanTypeProfile != null, getRepaymentStartDate() != null)
	then
		System.out.println("set the first due date");
		springContext.getBean(FirstDueDateCalculator.class).updateFirstDueDate($l);
		update($l);
end

rule "Update Effective Loan Type Profile"
	when
		$l : Loan(effectiveLoanTypeProfile == null)
		not LoanTypeProfileUpdated(loan.loanID == $l.loanID)
	then
		System.out.println("set the effective loan type profile");
		springContext.getBean(LoanTypeLogic.class).updateLoanTypeProfileForLoan($l);
		update($l);
end

rule "Set Starting Loan Term"
	when
		$l : Loan(initialUsedLoanTerm == null, effectiveLoanTypeProfile != null)
	then
		System.out.println("set the remaining loan term");
		$l.setInitialUsedLoanTerm(0);
		update($l);
end
