package loan

import org.gsoft.openserv.domain.loan.Loan
import org.gsoft.openserv.buslogic.amortization.AmortizationLogic
import org.springframework.context.ApplicationContext
import org.gsoft.openserv.domain.interest.LoanRateValue
import org.gsoft.openserv.repositories.rates.LoanRateValueRepository
import org.gsoft.openserv.domain.loan.Account
import org.gsoft.openserv.buslogic.system.SystemSettingsLogic
import java.util.Arrays
import java.util.List
import java.util.Date

global ApplicationContext springContext

declare AmortizationScheduleChecked
	accountId : Long
	updateNeeded : boolean
end

rule "Load Missing Amortization Dates"
	when
		$l : Loan(effectiveLoanTypeProfile != null, repaymentStartDate != null)
		LoanInterestRateUpdated(loanId == $l.loanID)
		not AmortizationSchedulesUpdated(accountId == $l.account.accountID)
		not AmortizationScheduleChecked(accountId == $l.account.accountID)
	then
		System.out.println("Amortization Schedule Update Needed");
		Date sysDate = springContext.getBean(SystemSettingsLogic.class).getCurrentSystemDate();
		boolean invalidScheds = springContext.getBean(AmortizationLogic.class).invalidAmortizationSchedulesExist($l.getAccount().getAccountID(), sysDate);
		boolean missingScheds = springContext.getBean(AmortizationLogic.class).amortizationSchedulesMissing($l.getAccount().getAccountID(), sysDate);
		AmortizationScheduleChecked check = new AmortizationScheduleChecked($l.getAccount().getAccountID(), invalidScheds || missingScheds);
		insert(check);
end

rule "Generate Missing Amortization Schedules"
	when
		AmortizationScheduleChecked(updateNeeded == true, $aid : accountId)
		not AmortizationSchedulesUpdated(accountId == $aid)
	then
		System.out.println("Generating Missing Amortization Schedules");
		Date sysDate = springContext.getBean(SystemSettingsLogic.class).getCurrentSystemDate();
		springContext.getBean(AmortizationLogic.class).updateInvalidAmortizationSchedules($aid, sysDate);
		springContext.getBean(AmortizationLogic.class).createMissingAmortizationSchedules($aid, sysDate);
		AmortizationSchedulesUpdated flag = new AmortizationSchedulesUpdated($aid);
		insert(flag);
end
