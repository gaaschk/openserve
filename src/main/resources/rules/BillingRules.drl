package billing

import org.gsoft.openserv.domain.loan.Loan
import org.gsoft.openserv.domain.loan.Account
import org.gsoft.openserv.domain.payment.billing.BillingStatement
import org.gsoft.openserv.buslogic.payment.BillingStatementLogic
import org.gsoft.openserv.repositories.payment.BillingStatementRepository
import java.util.Date
import java.util.Calendar
import java.util.HashMap
import org.springframework.context.ApplicationContext
import loan.LoanDecorator
import loan.AmortizationScheduleChecked

global ApplicationContext springContext

declare LoanDueDateWrapper
	loanId : Long
	isStatementNeeded : boolean
end

rule "Build Loan DueDate Wrapper"
	when
		$l : Loan($loanId : loanID, account != null)
		$ld : LoanDecorator(loanId == $l.loanID, effectiveLoanProgramSettings != null)
		$a : Account() from $l.account
		not LoanDueDateWrapper(loanId == $loanId)
	then
		LoanDueDateWrapper lw = new LoanDueDateWrapper();
		lw.setLoanId($loanId);
		boolean isNeeded = springContext.getBean(BillingStatementLogic.class).isBillingStatementNeeded($l);
		lw.setIsStatementNeeded(isNeeded);
		insert(lw);
end
	
rule "Generate Billing Statement"
	when
		$lw : LoanDueDateWrapper(isStatementNeeded, $loanId : loanId)
		$l : Loan(loanID == $loanId)
		AmortizationScheduleChecked(accountId == $l.account.accountID, !updateNeeded)
	then
		System.out.println("Generating a billing statement");
		BillingStatementLogic logic = springContext.getBean(BillingStatementLogic.class);
		System.out.println("Logic is: " + logic);
		logic.createBillingStatement($l);
		boolean isStatementNeeded = logic.isBillingStatementNeeded($l);
		$lw.setIsStatementNeeded(isStatementNeeded);
		update($l);
		update($lw);
end
